include "./orth/std.orth"

const N 100 end
const BOARD_SIZE 100 end

mem board BOARD_SIZE end
mem display 1 end

// Initialise the last item in the board to 1
// last board item relative address

// value
1
// addr
BOARD_SIZE 1 - board +
!8

0 while dup N < do
    // print the board
    0 while dup BOARD_SIZE < do
        // idx board[idx]
        dup board + @8 if
            42
        else
            32
        end
        display !8
        1 display puts
        1 +
    end
    drop

    10 display !8
    1 display puts

    // update elements

    // pattern
    board     @8 1 shl
    board 1 + @8
    bor

    1 while dup BOARD_SIZE 1 - < do
        // idx pattern
        swap 1 shl 7 band
        // idx pattern
        over board + 1 + @8 bor
        // idx pattern idx (110 >> pattern) & 1
        over over 110 swap shr 1 band
        // idx pattern
        swap board + !8
        // pattern idx
        swap

        1 +
    end
    drop drop
    1 +
end
drop
