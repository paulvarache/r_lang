const FD_stdout 1 end
const SYS_write 1 end
const SYS_exit 60 end

// macros are deprecated
macro puts FD_stdout SYS_write syscall3 end
macro exit SYS_exit syscall1            end

const N 100 end
const BOARD_SIZE 100 end

mem board BOARD_SIZE end
mem display 1 end

// Set all to 0
0 while dup BOARD_SIZE < do
    dup board + 1 !
    1 +
end

0 while dup N < do
    // print the board
    0 while dup BOARD_SIZE < do
        // idx board[idx]
        dup board + @ if
            42
        else
            95
        end
        display swap !
        1 display puts
        1 +
    end
    drop

    display 10 !
    1 display puts

    // update elements

    // pattern
    board @ 1 shl
    board 1 + @
    bor

    1 while dup 1 BOARD_SIZE - < do
        // idx pattern
        swap 1 shl 7 band
        // idx pattern
        over board + 1 + @ bor
        // idx pattern idx (110 >> pattern) & 1
        over over 110 swap shr 1 band
        // idx pattern
        swap board + swap !
        // pattern idx
        swap

        1 +
    end
    drop drop
    1 +
end
drop
